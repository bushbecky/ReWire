The set nfprog is a subset of prog from the general ReWire Core grammar.

---

nfprog ::= datadecl* nfdecl*
   {- Note: one of the datadecls must be exactly "data React o i a is D a | P o (i -> React o i a) end" -}

--

nfdecl ::= contdecl | maindecl | bittydecl
   {- Note: exactly one maindecl -}

--

contdecl ::= contname '::' ty 'is' contbody 'end'
   {- Note: ty is at least arity 1, with the last argument being of the input
      type; all arguments must be "bitty" types.

      Arity of the contbody must match arity of the type.

      Codomain must be "React I O A" where I and O are the same as in all other
      contdecls/pausedecls and A is bitty. -}

contbody ::= \ x1 -> .. -> \ xn -> \ xi -> contexpr
   {- Note: n >= 0 -}

contexpr ::= P bittyexpr contcall
           | 'case' bittyexpr 'of' { [pat -> contexpr]* }

contcall ::= contname bittyexpr*
           | 'case' bittyexpr 'of' { [pat -> contcall]* }

--

maindecl ::= 'main' '::' ty 'is' 'P' bittyexpr (contname bittyexpr*) 'end'
   {- Note: type must be of the form React I O A -}

--

bittydecl ::= bittyname '::' ty 'is' bittybody 'end'
   {- Note: type must be of the form t1 -> ... -> tn, with every ti bitty. -}

bittybody ::= \ x1 -> .. -> \ xn -> bittyexpr
   {- Note: n >= 0, arity must match that of the bittydecl -}

bittyexpr ::= bittyname bittyexpr*  {- Note: fully applied -}
            | conname bittyexpr*    {- Note: fully applied -}
            | literal
            | 'case' bittyexpr 'of' { [pat -> bittyexpr]* }
   {- Note: all bittyexprs must have bitty type -}

--

"Bitty" type ::= any monomorphic, non-recursive data type whose fields are all
bitty types; or certain primitive types like Integer, once we get around to
implementing those.
